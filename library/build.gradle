apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.github.ben-manes.versions"

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 91
        versionName "4.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue("string", "lib_version", "${versionName}")
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    androidExtensions {
        experimental = true
    }
}

Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.tools.build:apksig:3.5.3'
    implementation 'org.bouncycastle:bcprov-jdk16:1.46'
    implementation 'commons-io:commons-io:2.6'
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.topjohnwu.libsu:core:2.5.1'
    implementation 'com.github.topjohnwu.libsu:busybox:2.5.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.61"
    implementation 'com.github.MFlisar:changelog:1.0.4'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'com.github.madrapps:pikolo:2.0.1'
    constraints {
        implementation('androidx.collection:collection:1.1.0') {
            because 'transitive dependencies are including an older version'
        }
    }
    implementation project(':tutorial-view')
}
